{

    const ast = require("./ast")
    // const types = require("./types")
    const pathSeparator = "/"

    function getLocation() {
        let { start, end, filename } = location()
        return new ast.Location({
            start: new ast.Position(start.line, start.column),
            end: new ast.Position(end.line, end.column),
            filename
        })
    }

    function isUpperCase(name) {
        let first = name[0]
        return first === first.toUpperCase()
    }

    function isTypeName(name) {
        return isUpperCase(name)
    }

    function getRelativePrefixSteps(relative) {
        if (relative === 0) {
            return []
        }
        if (relative == 1) {
            return ["."]
        }
        return new Array(relative - 1).fill("..")
    }

    function node(type, ...values) {
        // we assign again in case it was overridden by copy values
        let TypeConstructor = ast[type]
        if (TypeConstructor == null) {
            throw new Error(`Missing Type: ${type}`)
        }
        let result = new TypeConstructor(Object.assign({ location: getLocation() }, ...values))
        return result
    }

    function operation(left, operator, right) {
        if (right) {
            return node("BinaryExpression", { left, operator, right })
        }
        else {
            return node("UnaryExpression", { operator, left })
        }
    }

    function buildBinaryExpression(head, tail) {
        return tail.reduce(
            (left, [,operator,,right]) => {
                if (operator === "isnt") {
                    return operation(operation(left, "is", right), "not")
                }
                else {
                    return operation(left, operator, right)
                }
            }
            ,head
        )
    }

    function toMap(nodes, getKey = node => node.id.name) {
        return new Map(nodes.map(node => [getKey(node), node]))
    }

}

////////////////////////////////////////////////////////////////////////////////
//  Module
////////////////////////////////////////////////////////////////////////////////

Module
    = EOL? body:ModuleDeclaration* EOL EOF
    {
        let location = getLocation()
        let id = node("Declarator", { name: location.filename })
        return node("Program", { sourceType: "module", id, body: body.flat() })
    }

ModuleDeclaration
    = ImportDeclaration
    // / Declaration
    / Statement
    / ExportDeclaration

////////////////////////////////////////////////////////////////////////////////
//  ExportDeclaration
////////////////////////////////////////////////////////////////////////////////

ExportDeclaration
    = ExportNamedDeclaration
    / ExportDefaultDeclaration

ExportNamedDeclaration
    = ExportToken _ declaration:(Declaration / ImportDeclaration)
    { return node(declaration.constructor.name, { ...declaration, export: 1 }) }

ExportDefaultDeclaration
    = ExportToken _ isDefault:DefaultToken? _ value:(ClassDeclaration / OutlineExpression)
    {
        if (ast.ClassDeclaration.is(value)) {
            return node(value.constructor.name, { ...value, export: isDefault ? 2 : 1 })
        }
        // we NEED to have some Declarator for this default export.
        return node("VariableDeclaration", { kind: "let", id: node("Declarator", { name: "default"}), value, export: isDefault ? 2 : 1})
    }

////////////////////////////////////////////////////////////////////////////////
//  ImportDeclaration
////////////////////////////////////////////////////////////////////////////////

ImportDeclaration
    = ImportToken _ source:(StringLiteral / Path) _ specifiers:(InlineSpecifiers / OutlineSpecifiers)
    {
        if (Array.isArray(source) /* is Path */) {
            if (specifiers.length === 0) {
                // smart import
                let local = source[source.length - 1]
                let type = isTypeName(local.name) ? "ImportDefaultSpecifier" : "ImportNamespaceSpecifier"
                specifiers.push(node(type, { local }))
            }
            let path = source.map(id => id.name ?? id).join(pathSeparator)
            source = node("Literal", { value: path })
        }
        return node("ImportDeclaration", { specifiers, source })
    }

InlineSpecifiers
    = head: Specifier tail:(_ "," _ @Specifier)* EOL
    { return [head, ...tail] }

OutlineSpecifiers
    = EOL Indent @(Dent @Specifier EOL)* Outdent

Path
    = relative:(".")* head:Identifier tail:(_ "." _ @Identifier)*
    { return [...getRelativePrefixSteps(relative.length), head, ...tail] }

Specifier
    = ImportDefaultSpecifier
    / ImportNamespaceSpecifier
    / ImportSpecifier

ImportSpecifier
    = imported:Identifier local:(_ AsToken _ @Declarator)?
    { return node("ImportSpecifier", { imported, local: local ?? node("Declarator", imported) }) }

ImportDefaultSpecifier
    = DefaultToken _ AsToken _ local:Declarator
    { return node("ImportDefaultSpecifier", { local }) }
    / "as" _ local:Declarator
    { return node("ImportDefaultSpecifier", { local }) }

ImportNamespaceSpecifier
    = "*" _ "as" _ local:Declarator
    { return node("ImportNamespaceSpecifier", { local }) }

////////////////////////////////////////////////////////////////////////////////
//  Statements
////////////////////////////////////////////////////////////////////////////////

Statement
    = Declaration
    / AssignmentStatement
    // / OutlineExpression
    / IfStatement
    / ForOfStatement
    / ReturnStatement
    / WhileStatement
    / ExpressionStatement
    / ThrowStatement
    / TryStatement
    / SwitchStatement
    / PropertyStatement

SwitchStatement
    = SwitchToken _ discriminant:InlineExpression EOL
        Indent
            cases: (Dent @SwitchCase)+
        Outdent
    { return node("SwitchStatement", { discriminant, cases: cases.flat() }) }

SwitchTest
    = DefaultToken { return null }
    / InlineExpression

SwitchCase
    = head:SwitchTest tail:(_ "," _ @SwitchTest)* ":" _
        consequent: (value:Statement {return node("BlockStatement", { body: Array.isArray(value) ? value : [value]}) } / BlockStatement)
    {
        let tests = [head, ...tail]
        return tests.map((test, index) => node("SwitchCase", { test, consequent: index === tests.length - 1 ? consequent : null }))
    }

TryStatement
    = TryToken
        block:BlockStatement
        handler:(Dent @CatchClause)?
        finalizer:(Dent FinallyToken @BlockStatement)?
    { return node("TryStatement", { block, handler, finalizer }) }

CatchClause
    = CatchToken _ param:Pattern _ body:BlockStatement
    { return node("CatchClause", { param, body }) }

ThrowStatement
    = ThrowToken _ arg:OutlineExpression
    { return node("ThrowStatement", { argument: arg }) }

ExpressionStatement
    = expression:OutlineExpression
    { return node("ExpressionStatement", { expression }) }
    // also allow multiple expression statements separated by commas.
    // all statement consumers call .flat() so this will be handled.
    // / expressions:InlineExpressionList EOL
    // { return expressions.map(expression => node("ExpressionStatement", { expression })) }

Declaration
    = ClassDeclaration
    / VariableDeclaration

VariableDeclarationType
    = VarToken { return { kind: "var" } }
    / LetToken { return { kind: "let" } }
    / GetToken { return { kind: "get" } }
    / SetToken { return { kind: "set" } }
    // / PropertyToken { return { assignable: false, property: true }}
    // / MetaToken { return { assignable: false, meta: true }}

VariableModifiers
    = &StaticToken id:Identifier _ { return { static: id } }
    / &. { return {} }

VariableDeclaration
    = modifiers:VariableModifiers TypeToken _ id:Declarator _ "=" _ value:Type EOL
    { return node("VariableDeclaration", { kind: "type", id, value, ...modifiers }) }
    / meta:MetaProperties modifiers:VariableModifiers declarationType:VariableDeclarationType
        _ id:(Pattern / "[" _ @InlineExpression _ "]") _ type:(":" _ @Type)? _ value:('=' _ @OutlineExpression / EOL {return null})?
    { return node("VariableDeclaration", { ...declarationType, id, type, value, meta, ...modifiers }) }

MetaProperties
 = (@DynamicOutlineProperty Dent)*

DynamicOutlineProperty
    = '[' _ key:InlineExpression _ ']' value:(_ ":" _ @OutlineExpression / EOL { return null })
    { return node("KeyValuePair", {key, value, computed:true} ) }

// VariableMetaProperties
//     = EOL Indent @OutlineProperty+ Outdent

AssignmentOperator
    = "="
    / "+="
    / "-="
    / "*="
    / "/="

AssignmentStatement
    = left:InlineExpression _ operator:AssignmentOperator _ right:OutlineExpression
    {
        if (operator.length > 1) {
            right = operation(left, node("Identifier",{ name:operator.slice(0,-1) }), right)
        }
        return node("AssignmentStatement", {left, operator, right})
    }

BlockStatement
    = EOL Indent body:(Dent @Statement)+ Outdent
    { return node("BlockStatement", { body: body.flat() }) }

ForOfStatement
    = ForToken _ left:Pattern _ InToken _ right:InlineExpression body:BlockStatement
    { return node("ForOfStatement", { left: (node("VariableDeclaration", { id: left, kind: "const" })), right, body } ) }

WhileStatement
    = WhileToken _ test:InlineExpression body:BlockStatement
    { return node("WhileStatement", { test, body }) }

IfStatement
    = IfToken _ test:InlineExpression consequent:BlockStatement
        alternate:(Dent ElseToken _ @(BlockStatement / IfStatement))?
    { return node("IfStatement", { test, consequent, alternate }) }

ReturnStatement
    = ReturnToken EOL Indent Dent argument:OutlineExpression Outdent
    { return node("ReturnStatement", { argument }) }
    / ReturnToken _ argument:InlineExpression EOL?
    { return node("ReturnStatement", { argument }) }

////////////////////////////////////////////////////////////////////////////////
//  Patterns
////////////////////////////////////////////////////////////////////////////////

Pattern
    = Declarator
    / ArrayPattern
    / ObjectPattern

ArrayPattern
    = "[" _ element:RestElement _ "]"
    { return node("ArrayPattern", { elements: [element] }) }
    / "[" _ head:Pattern? tail:(_ "," _ !"." @Pattern?)* rest:(_ "," _ @RestElement)? _ "]"
    {
        let elements = [head || null, ...tail.map((x) => x || null)]
        if (rest) {
            elements.push(rest)
        }
        return node("ArrayPattern", { elements })
    }

RestElement
    = "..." argument:Declarator
    { return node("RestElement", { argument }) }

ObjectPattern
    = "{" _ head:AssignmentProperty tail:(_ "," _ @AssignmentProperty)* rest:(_ "," _ @RestElement)? _ "}"
    {
        let properties = [head, ...tail]
        if (rest) {
            properties.push(rest)
        }
        return node("ObjectPattern", { properties })
    }

AssignmentProperty
    = key:Identifier value:(_ ":" _ @Pattern)?
    { return node("Property", { key, value: value || node("Reference", key), shorthand: value == null } ) }

////////////////////////////////////////////////////////////////////////////////
//  Class
////////////////////////////////////////////////////////////////////////////////

ClassModifiers
    = DataToken _ { return { isData: true } }
    / &. { return {} }

ClassDeclaration
    = meta:MetaProperties
        modifiers:ClassModifiers isStruct:(ClassToken { return false } / StructToken { return true })
        _ id:Declarator params:TemplateParameters? _ baseClasses:Extends? EOL
        Indent
            declarations:(Dent @VariableDeclaration)*
        Outdent
        {
            return node("ClassDeclaration", {
                id, ...modifiers, isStruct, params: params || [], baseClasses: baseClasses || [],
                static: declarations.filter(d => d.static),
                instance: node("InstanceDeclarations", { declarations: declarations.filter(d => !d.static).map(d => d.patch({ instance: true })) })
            })
        }

TemplateParameters
    = "<" _ head:Parameter tail:(_ ',' _ @Parameter)* _ ">" { return [head, ...tail] }

Extends
    = ExtendsToken _ head:TemplateReference tail:(_ "," _ @TemplateReference)*
    { return [head, ...tail] }

////////////////////////////////////////////////////////////////////////////////
//  TypeExpression
////////////////////////////////////////////////////////////////////////////////

Type
    = type:UnionType
    {
        let isReference = ast.BinaryExpression.is(type) && type.operator === "is" && ast.Reference.is(type.right)
        return isReference ? type.right : node("TypeExpression", { value: type })
    }

UnionType
    = left:ConstrainedType right:(_ "|" _ @UnionType)?
    { return right ? node('BinaryExpression', { left, operator: "||", right }) : left }

ConstrainedType
    = left:Constraint right:(_ "&" _ @ConstrainedType)?
    { return right ? node('BinaryExpression', { left, operator: "&&", right }) : left }

Constraint
    = right:Literal
    { return node('BinaryExpression', { left: node('DotExpression'), operator: '==', right }) }
    / left:MemberExpression? _ operator:(">=" / "<=" / ">" / "<" / "==" / "!=" / "is") _ right:MemberExpression
    { return node('BinaryExpression', { left: left || node('DotExpression'), operator, right }) }
    / right:(FunctionType / Reference /* Regex */)
    { return node('BinaryExpression', { left:node('DotExpression'), operator: 'is', right }) }
    // / TypeofToken _ argument:Reference
    // { return node('UnaryExpression', { operator: "typeof", argument })}

FunctionType
    = params:FunctionParameters _ "=>" _ returnType:Type
    { return node("FunctionType", {params, returnType}) }
    / "(" _ @FunctionType _ ")"

TemplateArgument
    = Type
    // / InlineExpression

TemplateArguments
    = "<" _ head:TemplateArgument tail:(_ "," _ @TemplateArgument)*  _ ">"
    { return [head, ...tail] }

TemplateReference
    = reference:Reference args:(_ @TemplateArguments)?
    { return args ? node("TemplateReference", { reference, arguments: args }) : reference }

// Quantifier
//     = "?" { return [0,1] }
//     / "*" { return [0, null] }
//     / "+" { return [1, null] }
//     / Range

// Range
//     = from:DecimalInteger _ ".." _ to:DecimalInteger? { return [from, to || null] }
//     / ".." _ to:DecimalInteger { return [null, to] }
//     / value:DecimalInteger { return [value, value] }

// KeyValue
//     = key:Type value:(_ ':' _ @Type)? _ quantifier:Quantifier?
//         { return node('KeyValue', {key, value, quantifier} ) }

// CollectionType
//     = '[' _ ']'
//         { return node('CollectionType', {elements:[]}) }
//     / '[' _ head:KeyValue tail:(_ ',' _ @KeyValue)* _ ']'
//         { return node('CollectionType', {elements:[head, ...tail]}) }

////////////////////////////////////////////////////////////////////////////////
//  Operations
////////////////////////////////////////////////////////////////////////////////

UnaryOperator
    = "+" / "-" / NotToken / "!" / TypeofToken

UnaryExpression
    = MemberExpression
    / operator:UnaryOperator _ argument:MemberExpression { return operation(argument, operator) }

ExponentiationOperator "operator"
    = "**" / "<<" / ">>"

ExponentiationExpression
    = left:UnaryExpression right:(_ operator:ExponentiationOperator _ right:ExponentiationExpression {return {operator,right}})?
    { return right ? node("BinaryExpression", {left, operator:right.operator, right:right.right}) : left }

MultiplicativeOperator
    = "*" / "/" / "%"

MultiplicativeExpression
    = head:ExponentiationExpression tail:(_ MultiplicativeOperator _ ExponentiationExpression)*
    { return buildBinaryExpression(head, tail) }

AdditiveOperator
    = "+" / "-"

AdditiveExpression
    = head:MultiplicativeExpression tail:(_ AdditiveOperator _ MultiplicativeExpression)*
    { return buildBinaryExpression(head, tail) }

RelationalOperator
    = "<=" / ">=" / "<" / ">" / IsToken / IsntToken

RelationalExpression
    = head:AdditiveExpression tail:(_ RelationalOperator _ AdditiveExpression)*
        { return buildBinaryExpression(head, tail) }
    / tail:(_ RelationalOperator _ AdditiveExpression)
        { return buildBinaryExpression(node('DotExpression') , [tail]) }

EqualityOperator
    = "==" / "!="

EqualityExpression
    = head:RelationalExpression tail:(_ EqualityOperator _ RelationalExpression)*
    { return buildBinaryExpression(head, tail) }

LogicalAndOperator
    = "&"

LogicalAndExpression
    = head:EqualityExpression tail:(_ LogicalAndOperator _ EqualityExpression)*
    { return buildBinaryExpression(head, tail) }

LogicalOrOperator
    = "|" / XorToken

LogicalOrExpression
    = head:LogicalAndExpression tail:(_ LogicalOrOperator _ LogicalAndExpression)*
    { return buildBinaryExpression(head, tail) }

ConditionalOperator
    = "?"

ConditionalExpression
    = test:LogicalOrExpression
        body:(
            _ ConditionalOperator _ consequent:ConditionalExpression
            _ ':' _ alternate:ConditionalExpression
            { return [consequent,alternate] }
        )?
    { return body ? node('ConditionalExpression', { test, body }) : test }

////////////////////////////////////////////////////////////////////////////////
//  Functions
////////////////////////////////////////////////////////////////////////////////

Parameter
    = id:Pattern _ type:(":" _ @Type)? _ value:("=" !">" _ @InlineExpression)?
    { return node("Parameter", { id, type, value })}

FunctionParameters
    = "(" _ ")" { return [] }
    / id:Declarator { return [node("Parameter", { id } )] }
    / "(" _ head:Parameter tail:(_ ',' _ @Parameter)* _ ")" { return [head, ...tail] }

FunctionHead
    = async:AsyncToken? _ generator:"*"? id:(@Identifier &"(")? params:FunctionParameters? type:(_ ":" _ @Type)? _ "=>"
    { return { id, type, async: async != null, generator: generator != null, params: params || [] } }

InlineFunction
    = head:FunctionHead _ value:InlineExpression?
    { return node("FunctionExpression", { ...head, body: node("BlockStatement", { body: !value ? [] : [node("ReturnStatement", { argument: value } )] } ) } ) }

NamedInlineFunction
    = &Identifier @InlineFunction

OutlineFunction
    = head:FunctionHead _ body:BlockStatement
    { return node("FunctionExpression", { ...head, body }) }

NamedOutlineFunction
    = &Identifier @OutlineFunction

////////////////////////////////////////////////////////////////////////////////
//  Expressions
////////////////////////////////////////////////////////////////////////////////

OutlineExpression
    = OutlineExpressionStartsThisLine
    / EOL Indent Dent @OutlineExpressionStartsThisLine Outdent

OutlineExpressionStartsThisLine
    = OutlineYieldExpression
    / OutlineAwaitExpression
    / OutlineDoExpression // must be higher precedence than OutlineFunction
    / OutlineFunction
    / OutlineArrayExpression
    / OutlineObjectExpression
    // / OutlineMapExpression
    / OutlineCallExpression
    / OutlineNewExpression
    / OutlineStringLiteral
    / OutlineSpreadElement
    / OutlineOperation
    / @InlineExpression EOL

OutlineDoExpression
    = DoToken " " _ callee:OutlineFunction
    { return node("CallExpression", { callee, arguments: [] }) }

OutlineOperation
    =
        operator:("+" / "-" / "||" / "&&" / "|" / "&" / "**" / "*" / "/")
        operands:OutlineArguments
    {
        if (operands.length === 2 && operands.every(ast.Expression.is)) {
            return node("BinaryExpression", { left: operands[0], operator, right: operands[1] })
        }
        return node("OutlineOperation", { operator, operands })
    }

OutlineYieldExpression
    = YieldToken delegate:"*"? _ argument:OutlineExpression
    { return node("YieldExpression", { argument, delegate: delegate != null })}

OutlineAwaitExpression
    = AwaitToken _ argument:OutlineExpression
    { return node("AwaitExpression", { argument })}

OutlineArguments
    = EOL Indent args:(Dent @(OutlineExpression / Statement))+ Outdent
    { return args.flat() }

OutlineCallExpression
    = call:InlineExpression args:OutlineArguments
    & { return ast.CallExpression.is(call) }
    { return node("CallExpression", { ...call, arguments: [...call.arguments, ...args] }) }

OutlineNewExpression
    = NewToken _ callee:MemberExpressionNoCall args:(InlineArguments / OutlineArguments)
    { return node("CallExpression", { new: true, callee, arguments: args }) }

InlineExpression
    = InlineSpreadElement
    / InlineYieldExpression
    / InlineAwaitExpression
    / InlineFunction
    / InlineNewExpression
    / ConditionalExpression

InlineAwaitExpression
    = AwaitToken _ argument:InlineExpression
    { return node("AwaitExpression", { argument })}

InlineYieldExpression
    = YieldToken delegate:"*"? _ argument:InlineExpression
    { return node("YieldExpression", { argument, delegate: delegate != null })}

InlineNewExpression
    = NewToken _ callee:MemberExpressionNoCall args:InlineArguments
    { return node("CallExpression", { new: true, callee, arguments: args }) }

PrimaryExpression
    = Reference
    / Literal
    / ThisExpression
    / DotExpression
    / GroupExpression
    / InlineArrayExpression
    / InlineObjectExpression
    // / InlineMapExpression

GroupExpression
    = "(" _ @InlineExpression _ ")"

ThisExpression
    = ThisToken { return node('ThisExpression') }

DotExpression
    = "." property:Identifier
    { return node('MemberExpression',{object:node('DotExpression'),property,computed:false}) }
    / ".[" _ property:InlineExpression _ "]"
    { return node('MemberExpression',{object:node('DotExpression'),property,computed:true}) }
    / "."
    { return node('DotExpression') }

InlineExpressionList
    = head:InlineExpression tail:(_ "," _ @InlineExpression)*
    { return [head, ...tail] }

InlineArguments
    = "()" { return [] }
    / "(" _ @InlineExpressionList _ ")"
    // / _ @InlineExpressionList

ChainMemberComputed
    = optional:"?"? "[" _ property:InlineExpression _ "]"
    { return { property, computed:true, optional: optional != null } }

ChainMember
    = optional:"?"? _ "." property:Identifier
    { return { property, computed:false, optional: optional != null } }

ChainMemberCall
    = optional:"?"? args:InlineArguments
    { return { arguments:args, optional: optional != null } }

MemberExpression =
    head: PrimaryExpression
    tail: (ChainMemberComputed / ChainMember / ChainMemberCall)*
    {
        return tail.reduce(
            (left, right) => {
                if (right.arguments) {
                    return node("CallExpression", {callee:left, ...right})
                }
                else {
                    return node("MemberExpression", { object:left, ...right })
                }
            }
            ,head
        )
    }

MemberExpressionNoCall =
    head: PrimaryExpression
    tail: (ChainMemberComputed / ChainMember)*
    {
        return tail.reduce(
            (left, right) => node("MemberExpression", { object:left, ...right })
            ,head
        )
    }

////////////////////////////////////////////////////////////////////////////////
//  Literals
////////////////////////////////////////////////////////////////////////////////

InlineSpreadElement
    = "..." argument:InlineExpression
    { return node("SpreadElement", { argument }) }

OutlineSpreadElement
    = "..." argument:OutlineExpression
    { return node("SpreadElement", { argument }) }

ArrayElement
    = InlineSpreadElement
    / InlineExpression

ArrayElements
    = head:ArrayElement tail:(_ "," _ @ArrayElement)*
    { return [head, ...tail] }

InlineArrayExpression
    = "[" _ elements:ArrayElements? _ "]"
    { return node("ArrayExpression", { elements:elements || [] }) }
    / "|" _ elements:ArrayElements? _ "|"
    { return node("ArrayExpression", { elements:elements || [], isSet: true }) }

OutlineArrayArguments
    = EOL Indent args:(Dent @(@(OutlineExpression / @InlineExpressionList EOL) / @Statement))+ Outdent
    { return args.flat() }

OutlineArrayExpression
    = "[]" elements: OutlineArrayArguments
    { return node("ArrayExpression", { elements: elements.flat() }) }
    / "||" elements: OutlineArrayArguments
    { return node("ArrayExpression", { elements: elements.flat(), isSet: true }) }

OutlineObjectExpression
    = e:InlineObjectExpression properties:OutlinePropertyList
    { return node("ObjectExpression", { isMap: e.isMap, properties: [...e.properties, ...properties] }) }

// OutlineMapExpression
//     = e:InlineMapExpression properties:OutlineMapPropertyList
//     { return node("MapExpression", {properties: [...e.properties, ...properties] }) }

OutlineMapPropertyList
    = EOL Indent @(Dent @(OutlineMapProperty / Statement))+ Outdent

OutlineMapProperty
    = key:InlineExpression _ ":" _ value:OutlineExpression
    { return node("Property", { key, value, computed: true })}

OutlinePropertyList
    = EOL Indent @(Dent @(OutlineProperty / Statement))+ Outdent

PropertyStatement
    = property:(OutlineProperty / OutlineMapProperty)
    { return node("PropertyStatement", { property }) }

OutlineProperty
    = @InlineSpreadElement EOL
    / "[" _ key:InlineExpression _ "]" _ ":" _ value: OutlineExpression
    { return node("Property", { key, value, computed: true }) }
    / key:(@Identifier _ ":") _ value:OutlineExpression
    { return node("Property", { key, value }) }
    / GetToken _ value:NamedOutlineFunction
    { return node("Property", { key: value.id, value, kind: "get", method: true }) }
    / SetToken _ value:NamedOutlineFunction
    { return node("Property", { key: value.id, value, kind: "set", method: true }) }
    / value:NamedOutlineFunction
    { return node("Property", { key: value.id, value, method: true }) }
    / key:Identifier EOL
    { return node("Property", { key, value: node("Reference", key), shorthand: true }) }
    //  don't support inline lists on outline objects... it's harder to see specific
    // / @InlinePropertyList EOL

InlineMapProperty
    = key:InlineExpression _ ":" _ value:InlineExpression
    { return node("Property", { key, value, computed: true })}

InlineMapPropertyList
    = head:InlineMapProperty tail:(_ "," _ @InlineMapProperty)*
    { return [head, ...tail] }

InlineObjectExpression
    = "{" _ properties:InlinePropertyList? _ "}"
    { return node("ObjectExpression", {properties: properties || []}) }
    / "(" _ properties:InlineMapPropertyList? _ ")"
    { return node("ObjectExpression", { properties: properties || [], isMap: true }) }

InlineProperty
    = InlineSpreadElement
    / "[" _ key:InlineExpression _ "]" _ ":" _ value: InlineExpression
    { return node("Property", { key, value, computed: true }) }
    / key:(@Identifier _ ":") _ value:InlineExpression
    { return node("Property", { key, value }) }
    / GetToken _ value:NamedInlineFunction
    { return node("Property", { key: value.id, value, kind: "get", method: true }) }
    / SetToken _ value:NamedInlineFunction
    { return node("Property", { key: value.id, value, kind: "set", method: true }) }
    / value:NamedInlineFunction
    { return node("Property", { key: value.id, value, method: true }) }
    / key:Identifier
    { return node("Property", { key, value: node("Reference", key), shorthand: true }) }

InlinePropertyList
    = head:InlineProperty tail:(_ "," _ @InlineProperty)*
    { return [head, ...tail] }

Literal
    = NumericLiteral
    / BooleanLiteral
    / StringLiteral
    // / CharLiteral
    / NullLiteral
    / RegularExpressionLiteral

NumericLiteral
    = HexIntegerLiteral
    / DecimalLiteral
    / DecimalIntegerLiteral

NullLiteral
    = NullToken { return node('Literal', { value: null }) }

BooleanLiteral
    = value: (TrueToken / FalseToken) { return node('Literal', { value: JSON.parse(value) ? true : false } ) }

ExponentPart
    = 'e' [+-]? [0-9]+ !IdPart

DecimalLiteral
    = (DecimalInteger? '.' [0-9]+ ExponentPart? / DecimalInteger ExponentPart) !IdPart
    { return node('Literal', { value: parseFloat(text()) })}

HexDigit
    = [0-9A-F]

HexIntegerLiteral
    = '0x' HexDigit+ !IdPart { return node('Literal', {value: parseInt(text(), 16) }) }

DecimalIntegerLiteral
    = DecimalInteger { return node('Literal', { value: parseInt(text()) }) }

DecimalInteger
    = '0' / [+-]? [1-9] [0-9]* !IdPart { return parseInt(text()) }

StringLiteral
    = '"' DoubleStringChar* '"' { return node('Literal', { value: eval(text()) }); }
DoubleStringChar
    = [^\r\n\t\b\f"] / "\\" ([rn"] / "u" HexDigit<4>)

// CharLiteral
//     = "'" DoubleStringChar* "'" { return node('CharLiteral', eval(text())); }

OutlineStringLiteralLine
    = _ LineTerminatorSequence { return '' }
    / Dent line:[^\r\n]* LineTerminatorSequence { return line.join('') }

OutlineStringLiteral
    = '""' EOL Indent lines:OutlineStringLiteralLine* Outdent
    { return node('Literal', { value: lines.join('\n') }) }

////////////////////////////////////////////////////////////////////////////////
//  Regular Expressions
////////////////////////////////////////////////////////////////////////////////

RegularExpressionLiteral
    = "/" pattern:$((("\\" .) / [^/])+)  "/" flags:$([a-z]*)
    { return node("RegularExpression", {pattern, flags }) }

////////////////////////////////////////////////////////////////////////////////
//  Identifiers
////////////////////////////////////////////////////////////////////////////////

IdStart
    = [a-zA-Z$_]

IdPart
    = IdStart / [0-9]

IdName
    = $(!ReservedWord IdStart IdPart*)
    / "`" @($((!"`" .)*)) "`"

Identifier
    = name:IdName { return node('Identifier', { name }) }

Reference
    = name:IdName {
        return node("Reference", { name })
    }

Declarator
    = name:IdName {
        return node("Declarator", { name })
    }


////////////////////////////////////////////////////////////////////////////////
//  Tokens and Keywords
////////////////////////////////////////////////////////////////////////////////

ThisToken       = @"this"       !IdPart
NullToken       = @"null"       !IdPart
TrueToken       = @"false"      !IdPart
FalseToken      = @"true"       !IdPart
TypeToken       = @"type"       !IdPart
IsToken         = @"is"         !IdPart
IsntToken       = @"isnt"       !IdPart
AsToken         = @"as"         !IdPart
AndToken        = @"and"        !IdPart
OrToken         = @"or"         !IdPart
XorToken        = @"xor"        !IdPart
NotToken        = @"not"        !IdPart
TypeofToken     = @"typeof"     !IdPart
NewToken        = @"new"        !IdPart
WhereToken      = @"where"      !IdPart
ImportToken     = @"import"     !IdPart
ExportToken     = @"export"     !IdPart
DefaultToken    = @"default"    !IdPart
ClassToken      = @"class"      !IdPart
StructToken     = @"struct"     !IdPart
VarToken        = @"var"        !IdPart
LetToken        = @"let"        !IdPart
PropertyToken   = @"property"   !IdPart
MetaToken       = @"meta"       !IdPart
StaticToken     = @"static"     !IdPart
DataToken       = @"data"       !IdPart
ExtendsToken    = @"extends"    !IdPart
ForToken        = @"for"        !IdPart
WhileToken      = @"while"      !IdPart
InToken         = @"in"         !IdPart
OfToken         = @"of"         !IdPart
IfToken         = @"if"         !IdPart
ElseToken       = @"else"       !IdPart
ReturnToken     = @"return"     !IdPart
BreakToken      = @"break"      !IdPart
ContinueToken   = @"continue"   !IdPart
AsmToken        = @"asm"        !IdPart
ThrowToken      = @"throw"      !IdPart
TryToken        = @"try"        !IdPart
CatchToken      = @"catch"      !IdPart
FinallyToken    = @"finally"    !IdPart
GetToken        = @"get"        !IdPart
SetToken        = @"set"        !IdPart
SwitchToken     = @"switch"     !IdPart
AsyncToken      = @"async"      !IdPart
AwaitToken      = @"await"      !IdPart
YieldToken      = @"yield"      !IdPart
DoToken         = @"do"         !IdPart

Keyword
    = ThisToken
    / NullToken
    / TrueToken
    / FalseToken
    / AndToken
    / IsToken
    / IsntToken
    / AsToken
    / OrToken
    / XorToken
    / NotToken
    / TypeofToken
    / NewToken
    / WhereToken
    / ClassToken
    / StructToken
    / VarToken
    / LetToken
    / ForToken
    / WhileToken
    / IfToken
    / ElseToken
    / ReturnToken
    / BreakToken
    / ContinueToken
    / ExportToken
    / ImportToken
    / AsmToken
    / ThrowToken
    / TryToken
    / CatchToken
    / FinallyToken
    / SwitchToken
    // / GetToken
    // / SetToken

ReservedWord
    = Keyword

////////////////////////////////////////////////////////////////////////////////
//  Indentation
////////////////////////////////////////////////////////////////////////////////

Indent = indentation++
Outdent = indentation--
Dent = "    "<indentation>

////////////////////////////////////////////////////////////////////////////////
//  Whitespace
////////////////////////////////////////////////////////////////////////////////

LineTerminatorSequence
    = "\n"
    / "\r\n"
    / "\r"

Comment ""
    = "#" (![\r\n] .)*

_ ""
    = ' '*

__ ""
    = (' ' / LineTerminatorSequence / Comment)*

EOL "end of line"
    = (_ Comment? LineTerminatorSequence+)+ / EOF

EOF "" = !.
